DEFINE REGS 3, 15, 40
! DEFINE REGS 3, 40

<cmath>
<algorithm>
"PlotTools/interface/KinematicFunctions.h"
"PandaTree/Objects/interface/Event.h"
"misc.h"

{/F}                           ! Default type
{0}                            ! Default value
{reset(panda::Event& event)}   ! Define the arguments of reset

$/i = event.$ | runNumber, lumiNumber
eventNumber/l = event.eventNumber

npv/s = event.npv
npv_true/s = event.npvTrue

mc_weight = event.weight

$@ = event.$@@.pt | pf, calo, trk |@ met
$@phi = event.$@@.phi | pf, calo, trk |@ met

#dztrkmetvec/TVector2 = trkmet::trkmet(event)
dztrkmet <- dztrkmetvec.Mod()
dztrkmetphi <- dztrkmetvec.Phi()

hbb_2016_trigger/O = false
met_trigger/O = false
met_filter/O = event.metFilters.pass()

countjets(panda::Jet& jet, float abseta, lazy::LazyCuts& cmva, lazy::LazyCuts& csv)
  jet_$[] -> jet.$() | pt, eta, phi
  #? jet_pt[]; jetpt ? -> jet.ptCorr%%

! Count some objects
{/b}

  n_alljet -> ++
  n_allhard -> +(jet_pt[] > 30.0)

  min_dphi_metj_hard/F = 4 -> jet_pt[] > 30.0 ? std::min(min_dphi_metj_hard, static_cast<Float_t>(deltaPhi(pfmetphi, jet.phi()))) : min_dphi_metj_hard

  ~~ abseta < 2.5 ~~

  n_softjet -> ++
  n_jet -> +(jet_pt[] > 30.0)
  n_centerjet -> +(jet_pt[] > 30.0 && abseta < 2.4)
  n_b@_$ -> +(@.$() && jet_pt[] > 25.0 && abseta < 2.4) | loose, medium, tight |@ cmva, csv

n_genB

n_soft {
  , _all {
    softcount(float pt)
      $ -> +(pt > $) | 2, 5, 10
      ? $ ; softcount ? -> +(pt * (1.0 +- 0.1) > $) | 2, 5, 10
  }
}

n_pho {
  medium
  tight
  +n_tau {
    loose
  }
}

{/F}

! Save the highest pT of tight lepton
tight_lep_pt

muon, ele {
  lep(panda::Lepton& lep, LepInfo info, lazy::LazyCuts& cuts)
    ! Don't save the rest of the stuff for anything that's not loose
    ! but count presel here
    ~~ cuts.$() ~~ \\ count_$/b -> ++ | presel, loose

    $[] -> info.$ | reliso, minireliso, corrpt
    q[]/B -> lep.charge

    pog_$[]/O = false -> lep.$ | loose, medium, tight
    $[]/O = false -> cuts.$() | loose, medium, tight
    $[] -> lep.$() | pt, eta, phi

    ~~ cuts.$() ~~ \\ count_$/b -> ++ | medium, tight
}

n_lep_$/b <- <{muon + ele}_count_$> | presel, loose, medium, tight

! Some calculated things

"TVector2.h"

{fill(TVector2 recoilvec)}

recoil <- recoilvec.Mod()
recoilphi <- recoilvec.Phi()

! Minimum dphi between MET and b-jet (to be declared)
min_dphi_recoilb <- std::min(<deltaPhi(cmva_jet{1, 2}_phi, recoilphi)>)

dphi_met_$met <- deltaPhi(<{pf, $}metphi>) | trk, dztrk

bsf(panda::Jet& jet)
  ! Hold some values temporarily. Should implement local variables some day

  (scale = btag::get_scale(jet))

  n_allbjet/b -> ++

  bjet_$[] -> jet.$() | pt, eta
  #bjet_$[] -> jet.$() | phi
  bjet_$[] -> jet.$ | cmva

DEFINE BSYST jes, lf, hf, <{hfstats, lfstats, cferr}{1, 2}>

  bjet_sf[] -> scale["central"]
  ? bjet_sf[]; $ ? -> scale["$%%"] | JES, LF, HF, <{cErr, Stats}{1, 2}>

  btag_sf = 1 -> *(bjet_sf[])

! Input jets are used to set the following variables
cmva {
  <_jet{1, 2, 3}> {

! If this is at the top, we don't save all of the systematics from jet pt

DEFINE bkinematics leadlep, vtx, <puppi_{charged, neutral}{, _pu}>, <{em, neut, ch, mu}_r{0..5}>

    @_$frac <- []_@_$/[]_$ | pt, m, e |@ bkinematics
    @_d$ <- TVector2::Phi_mpi_pi([]_@_$ - []_$) | eta, phi |@ bkinematics

    bjet(panda::Jet& jet, float maxpt, <T> nlep, panda::PFCand* lep)

      /O = false -> true
      $ -> jet.$() | pt, eta, phi, m, e
      ? pt; jetpt; e, m ? -> jet.ptCorr%%   ! Indicate pt uncertainty

      #p4/TLorentzVector = TLorentzVector() -> [&] () { TLorentzVector p4; p4.SetPtEtaPhiM(<[]_{pt, eta, phi, m}>); return p4; } ()

      $ -> []_p4.$$() | et, mt

      loosepu/O = false -> puid::loose(jet)

      $ -> jet.$ | puid, chf, nhf, cef, nef, ptSmear

      ? ptSmear; jetsmear ? -> jet.ptSmear%%

      ! Set the taggers to -2 to disappear them from plots
      $ = -2 -> jet.$ | qgl, csv, cmva ! , deepCSVb, deepCMVAb

      $/O = false -> jet.$ | loose, tight, monojet

      $frac <- <[]_{c + n}$f> | e, h  ! EM and Had Fractions

      (flav = btag::flavor(jet.matchedGenJet))
      maxtrk -> maxpt

      ! Regression stuff
      (puppi = regression::GetJetInfo(jet))
      puppi_@_$ -> puppi.@.$$() | pt, eta, phi, m, e |@ <{charged, neutral}{, _pu}>

      $_r@_energy -> puppi.$_rings[@].first | em, neut, ch, mu |@ 0..5
      @_rX_$ -> puppi.@_rings[X].second.$$() | pt, eta, phi, m, e |@ em, neut, ch, mu |X 0..5

      $ -> puppi.$ | ptD
      $/s -> puppi.$ | num03

      ~~ lep ~~

      nlep/b -> nlep
      leadlep_$ -> lep->$() | pt, eta, phi, m, e
      leadlep_ptrel = -1 -> lep->p4().Perp(jet.p4().Vect())
      leadlep_dr = -1 -> deltaR(<{lep->, jet.}{eta, phi}()>)

    bvert(panda::Ref<panda::SecondaryVertex>& vert)
      ~~ vert.isValid() ~~

      vtx_$ -> vert->$() | pt, eta, phi, m, e

      vtx_3Dval -> vert->vtx3DVal
      vtx_3Derr -> vert->vtx3DeVal
      vtx_ntrk/s -> vert->ntrk

    ! Some 4-vector info for training

    genjet(panda::GenJet& gen, GenNuVec& withnu)
      gen/O = false -> true
      gen_$ -> gen.$() | pt, eta, phi, m
      gen_withnu_$ -> withnu.genvec.$$() | pt, eta, phi, m
      gen_numnu/b -> withnu.numnu
      gen_tWnu_overlap/O = false -> withnu.overlap

      gen_$/S -> gen.$ | pdgid, partonFlavor
      gen_$/b -> gen.$ | numB

    reclustered(fastjet::PseudoJet* jet)
      ~~ jet ~~
      reclustered/O = false -> true
      reclustered_$ -> jet->$() | pt, eta, phi, m

    [pt_ratio_old; data/TMVARegression_BDT.weights.xml; data/bjet_old_sub.txt] = 1
    [[tf_ptratio, tf_deta, tf_dphi, tf_mratio; ../analysis/weights/output4.pb; ../analysis/regression_branches.txt; crombie_input; crombie_output/BiasAdd]]

    ! Need a more systematic name system

    pt_reg$ <- []_pt_ratio$ * []_pt | _old ! , _old_method
    $_tf <- []_tf_$ratio * []_$ | pt, m
    $_tf <- []_tf_d$ + []_$ | eta, phi
  }

  daughter_$_pt <- std::$(<[]_jet{1, 2}_pt>) | min, max

"TLorentzVector.h"
  _hbb {
    hbb()
      #vec/TLorentzVector = TLorentzVector() -> vectorSum(<[^]_jet{1, 2}_{pt, eta, phi, m}>)
      /O = false -> true
      $ -> []_vec.$$() | pt, eta, phi, m

      m_reg$ <- vectorSumMass(<[^]_jet{1, 2}_{pt_reg$, eta, phi, m}>) | _old ! , _old_method
      pt_reg$ <- vectorSumPt(<[^]_jet{1, 2}_{pt_reg$, phi}>) | _old ! , _old_method

      m_tf <- vectorSumMass(<[^]_jet{1, 2}_{pt, eta, phi, m}_tf>)
      pt_tf <- vectorSumPt(<[^]_jet{1, 2}_{pt, phi}_tf>)

      reg$_over_m <- []_m_reg$/[]_m | _old ! , _old_method

    hbbgen(panda::GenParticle& gen, float dr)
      gen/O = false -> true
      gen_$ -> gen.$() | pt, eta, phi, m

      gen_dr = 10 -> dr

    pt_over_vpt <- []_pt/recoil
  }

  ! Need a more systematic name system

  $_dijet_m <- vectorSumMass(<[]_jet{1, 2}_$_{pt, eta, phi, m}>) | gen, gen_withnu, reclustered
  dphi_uh <- deltaPhi([]_hbb_phi, recoilphi)

  daughter_deta <- std::abs([]_jet1_eta - []_jet2_eta)
  daughter_dphi <- deltaPhi(<[]_jet{1, 2}_phi>)
  daughter_dR <- deltaR(<[]_jet{1, 2}_{eta, phi}>)
}

{/b}

countfat(panda::FatJet& jet)
  n_fatjet -> +(jet.pt() > 200)
  ? n_fatjet; jetpt ? -> +(jet.ptCorr%% > 200)

{/F}

! Fat jets are simple for now
ak8, ca15 {
  fatjet1 {
    fatjet(panda::FatJet& jet, <T> n_iso, <T> n_iso_b)
      /O = false -> true
      $ -> jet.$() | pt, eta, phi, m, e
      ? pt; jetpt ? -> jet.ptCorr%%
      $/O = false -> jet.$ | loose, tight, monojet
      $ -> jet.$ | mSD, mPruned, <tau{1, 2, 3}{, SD}>

      ! Knock double_sub off of the resolved plots
      $ = -2 -> jet.$ | double_sub

      $/b -> $ | n_iso, n_iso_b

      mSD_corr -> []_mSD * corrections::mSD([]_pt, []_eta)

      ! This is just used for some filtering
      #good/O = false -> []_pt > 220 and []_mSD_corr > 25

      dphi_u <- deltaPhi([]_phi, recoilphi)
      dR_resolved_hbb <- deltaR(<{[], cmva_hbb}_{eta, phi}>)
  }
}

genboson, gen_t, gen_tbar {
  gen(panda::GenParticle& gen)
    /O = false -> true
    $ -> gen.$() | pt, eta, phi, m

    pdgid/I -> gen.pdgid
}

! pandaf/009
! pdf = 1.0
! ? pdf; pdf ? = 1.0 +- std::abs(event.genReweight.pdfDW)

! pandaf/010
! pdf_$ = event.genReweight.pdfAltDW[$] | 0..100

r$f@DW = event.genReweight.r$f@DW | 1 |@ 2, 5
r$f@DW = event.genReweight.r$f@DW | 2, 5 |@ 1, $

! [event_class$; data/Classifier_m$_BDT.weights.xml] | , _reg_3, _reg_40
! [boost_class; data/Classifier_wboost_BDT.weights.xml; data/boosted_sub.txt]
#[maier_event_class; data/Hbb_BDTG.weights.xml; data/bdt_translate.txt]

! Filter
! These are filled before the fill function
-- debug::debugevent or (pfmet > 150 or recoilvec.Mod() > 150) --
! These are filled in the fill function
++ debug::debugevent or (ak8fatjet1 or ca15fatjet1 or (cmva_daughter_max_pt > 50 and cmva_daughter_min_pt > 25)) ++
